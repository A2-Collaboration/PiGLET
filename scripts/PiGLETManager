#!/usr/bin/perl
use strict;
use warnings;

#use Data::TreeDumper;
use Getopt::Long;
use Pod::Usage;
use Switch;
use Net::Telnet;

# define the display sets $dispsets here
# as a hashref, the key is the "name" of the dispset
# please provide a meaningful description of the displayset,

my $dispsets = {};

# define display sets CBTaggTAPS
$dispsets->{Standard} =
  {
   'Description' => 'Counting Room Standard',
   'Displays' => [
                  {
                   'Hostname' => 'a2onlinedisplay-00',
                   'Windows' => [
                                 {Type=>'Image', Name=>'Radiator', URL=>'http://a2radiator2.online.a2.kph/free.jpg'}
                                ]
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-01',
                   'Windows' => [
                                 {Type=>'Plot',  Name=>'DAQ:CBESum', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:CBESum_High', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:CBESum_Low', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:PbGlass', 'BackLength'=>600}
                                ]
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-02',
                   'Windows' => [
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-exptrigger', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-cb-adc-1a', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-cb-adc-1b', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-cb-adc-2a', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-cb-adc-2b', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-cb-tdc-a', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-cb-tdc-b', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-tagg-tdc-a', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-tagg-tdc-b', 'BackLength'=>600}
                                ]
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-03',
                   'Windows' => [
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_TAPSDetector', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-beampolmon', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-mwpc-adc-a', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-mwpc-adc-b', 'BackLength'=>600}
                                ]
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-04',
                   'Windows' => []
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-05',
                   'Windows' => []
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-06',
                   'Windows' => []
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-07',
                   'Windows' => []
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-08',
                   'Windows' => []
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-09',
                   'Windows' => []
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-10',
                   'Windows' => []
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-11',
                   'Windows' => []
                  }
                 ]
  };


# some default config options
# and provide nice help documentation
# some global variables, needed everywhere

my $man = 0;
my $help = 0;
my $verbose = 0;
my $warnings = 1;
my $exact = 0;
my $dispsetfile;
my $t; # telnet connection

Getopt::Long::Configure(qw(gnu_getopt));
GetOptions(
           'help|h' => \$help,
           'man' => \$man,
           'verbose|v+' => \$verbose,
           'warnings|w!' => \$warnings,
           'exact|e+' => \$exact,
           'dispsetfile|d=s' => \$dispsetfile
          ) or pod2usage(2);
pod2usage(1) if $help;
pod2usage(-exitval => 0, -verbose => 2) if $man;

if ($verbose) {
  # always enable warnings if verbose
  $warnings = 1;
}

# this loads some additional dispsets
&LoadDispsetfile;


# jump to subroutine which handles the job,
# depending on the options
my $mode = shift @ARGV || "";

switch($mode) {
  case "activate" { &Activate; }
  case "list" { &List; }
  else {
    Logger("Supplied mode not recognized, try --help or --man");
  }
}

sub Activate {
  my $dispset = $ARGV[0] || "";
  Logger("Specified dispset $dispset not found")
    unless defined $dispsets->{$dispset};

  Logger("Can't be that exact!")
    if $exact>3;
  Logger("Activating display set $dispset",1);
  # define a shortcut to the actual dispset data
  my $d = $dispsets->{$dispset};

  foreach my $disp (@{$d->{Displays}}) {
    WorkOnDisplay($disp);
  }

  Logger("Display set $dispset activated.",1);
}

sub List {
  print "The following display sets are available:\n";
  foreach my $dispset (sort keys $dispsets) {
    printf("* %-10s\t%s\n",$dispset,,$dispsets->{$dispset}->{Description});
  }
}



# helper functions

sub LoadDispsetfile {
  return unless defined $dispsetfile;
  do $dispsetfile or Logger("Can't load dispsetfile $dispsetfile: $!",-1);
}


sub WorkOnDisplay {
  my $disp = shift;
  my $host = $disp->{Hostname};
  Logger("$host: Opening connection",2);

  $t = Net::Telnet->new(Timeout => 5, Port => 1337);
  $t->errmode("return");

  $t->open($host)
    or Logger("$host: Cannot open telnet connection: ".$t->errmsg);

  $t->waitfor(String => 'Welcome to PiGLET!')
    or Logger("$host: Did not receive welcome message: ".$t->errmsg);

  Logger("$host: Successfully connected.",2);

  # get the current windows via the List command.
  # search for words ending in _Remove, which
  # indicate the unique name of each window
  $t->print('List') or
    Logger("$host: Cannot send List command: ".$t->errmsg);
  $t->waitfor(String => 'Available commands:') or
    Logger("$host: Did not receive list of windows: ".$t->errmsg);
  my @words = split(/\s+/,$t->lastline);
  my %wins =
    map { s/(.+)_Remove/$1/ ? ($_ => 1) : () } @words;

  my $n = scalar keys %wins;
  my $n_exp = scalar @{$disp->{Windows}};
  Logger("$host: Currently $n windows active, $n_exp in display set.",2);

  if($exact==3) {
    Logger("$host: Removing all windows.",2);
    ExecCmd("RemoveAllWindows");
  }

  # let's check if it complies with the display set
  foreach my $win (@{$disp->{Windows}}) {
    my $name = $win->{Name}; # unique identier for windows
    if($exact==3 || !exists $wins{$name}) {
      Logger("$host: Window $name not found. Adding it.", 2);
      my $type = $win->{Type}; # Plot, Image, ...?
      ExecCmd("Add${type}Window $name");
      SetWindowProperties($win);
    }
    elsif($exact>=1) { # equals $exact==1 || $exact==2
      # ensure correct properties
      SetWindowProperties($win);
    }
    delete $wins{$name};
  }

  # remove "unknown" windows
  if($exact == 2) {
    foreach my $name (keys %wins) {
      Logger("$host: Removing unknown window $name", 2);
      ExecCmd("${name}_Remove");
    }
  }
}

sub SetWindowProperties {
  my $win = shift;
  my $host = $t->host;
  my $name = $win->{Name};
  foreach my $prop (keys $win) {
    # skip "special" properties
    next if $prop eq 'Name';
    next if $prop eq 'Type';
    my $val = $win->{$prop};
    Logger("$host: Setting ${name}_$prop to $val.", 2);
    ExecCmd("${name}_$prop $val");
  }
}

sub ExecCmd {
  my $host = $t->host;
  my $cmd = shift;
  #$t->dump_log('STDERR');
  $t->print($cmd) or
    Logger("$host: Cannot send Cmd '$cmd': ".$t->errmsg);
  my $patt = '/(Ok.|Error:)/';
  $t->waitfor($patt) or
    Logger("$host: Did not receive 'Ok.' or 'Error:' after '$cmd': ".$t->errmsg);
  if($t->lastline =~ /Error:/) {
    Logger("Cmd '$cmd' was not successful: ".$t->lastline);
  }
}

sub Logger {
  my $msg = shift;
  my $lvl = shift || 0; # 0=fatal, -1=warning, >0 verbose messages

  if ($lvl==0) {
    print STDERR "Error: $msg\n";
    exit 1;
  }
  elsif($lvl==-1) {
    print STDERR "Warning: $msg\n" if $warnings;
  }
  else {
    print "$msg\n" if $lvl<=$verbose;
  }
}

__END__

=head1 NAME

PiGLETManager - Interface to PiGLET (Raspberry Pi OpenGL EPICS Toolkit)

=head1 SYNOPSIS

PiGLETManager activate <displayset>

PiGLETManager list

 Options:
   -h, --help         brief help message
   -v, --verbose      be verbose to STDERR
   -w, --warnings     print warnings to STDERR
   -e, --exact        set display more precisely
   -d, --dispsetfile  load extra display sets

=head1 OPTIONS

=over 8

=item B<--help>

Print a brief help message and exits.

=item B<--verbose>

Print some information what is going on. Default off.

=item B<--warnings>

Print warnings (or "not-so-fatal" errors). Default on.

=item B<--exact>

If specified once, the BackLength's are reset even if the window
exists.

If specified twice (e.g. -ee), then additionally unknown windows are
removed.

If specified three times (e.g. -eee), everything is removed and added
from scratch. Note: This also deletes the history of the graphs and
reloads the metadata like yrange from EPICS.

=item B<--dispsetfile>

Load more dispsets from the specified file. It must be valid PERL
syntax which extends or modifies the hashref $dispsets, see the
beginning of the source code of PiGLETManager how the default dispsets
are defined.


=back

=head1 DESCRIPTION

=cut
