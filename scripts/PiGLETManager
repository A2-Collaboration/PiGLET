#!/usr/bin/perl
use strict;
use warnings;

#use Data::TreeDumper;
use Getopt::Long;
use Pod::Usage;
use Switch;
use Net::Telnet;
use Module::Load;
use threads;


# define the display sets $dispsets here
# as a hashref, the key is the "name" of the dispset
# please provide a meaningful description of the displayset,

my $dispsets = {};

# define display sets

$dispsets->{Test} =
  {
   'Description' => 'Messing around on localhost...',
   'Displays' => [
                  {
                   'Hostname' => 'localhost',
                   'Windows' => [
                                 {
                                  Type=>'Plot', 'Name'=>'MyTestRecord0', 'BackLength'=>600},
                                 {
                                  Type=>'Plot', 'Name'=>'MyTestRecord1', 'BackLength'=>100},
                                 {
                                  Type=>'Image', 'Name'=>'Webcam',
                                  Crop=>[260, 280, 75, 125],
                                  Crosshair=>[100, 100, 30]
                                 }
                                ]
                  }
                 ]
  };


$dispsets->{Standard} =
  {
   'Description' => 'Counting Room Standard',
   'Displays' => [
                  {
                   'Hostname' => 'a2onlinedisplay-00',
                   'Windows' => [
                                 {Type=>'Image', Name=>'Radiator',
                                  Crop=>[260, 280, 75, 125],
                                  Crosshair=>[100, 100, 30],
                                  URL=>'http://a2radiator2.online.a2.kph/free.jpg'}
                                ]
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-01',
                   'Windows' => [
                                 {Type=>'Plot',  Name=>'DAQ:CBESum', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:CBESum_High', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:CBESum_Low', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:PbGlass', 'BackLength'=>600}
                                ]
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-02',
                   'Windows' => [
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-exptrigger', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-cb-adc-1a', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-cb-adc-1b', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-cb-adc-2a', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-cb-adc-2b', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-cb-tdc-a', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-cb-tdc-b', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-tagg-tdc-a', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-tagg-tdc-b', 'BackLength'=>600}
                                ]
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-03',
                   'Windows' => [
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_TAPSDetector', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-beampolmon', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-mwpc-adc-a', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:Livetime_vme-mwpc-adc-b', 'BackLength'=>600}
                                ]
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-04',
                   'Windows' => [
                                 {Type=>'Plot',  Name=>'DAQ:TAPS_Debug_ActualState_0', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:TAPS_Debug_ActualState_1', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:TAPS_Debug_ActualState_2', 'BackLength'=>600},
                                 {Type=>'Plot',  Name=>'DAQ:TAPS_Debug_ActualState_3', 'BackLength'=>600}
                                ]
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-05',
                   'Windows' => [
                                 {Type=>'Plot',  Name=>'CB:MON:TEMP:TOP', 'BackLength'=>7200},
                                 {Type=>'Plot',  Name=>'CB:MON:TEMP:BOT', 'BackLength'=>7200},
                                 {Type=>'Plot',  Name=>'CB:MON:PRES:TOP', 'BackLength'=>7200},
                                 {Type=>'Plot',  Name=>'CB:MON:PRES:BOT', 'BackLength'=>7200}
                                ]
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-06',
                   'Windows' => [
                                 {Type=>'Plot',  Name=>'MWPC:HV:CH1:CURR:READ', 'BackLength'=>1800},
                                 {Type=>'Plot',  Name=>'MWPC:HV:CH1:VOLT:READ', 'BackLength'=>1800},
                                 {Type=>'Plot',  Name=>'MWPC:HV:CH2:CURR:READ', 'BackLength'=>1800},
                                 {Type=>'Plot',  Name=>'MWPC:HV:CH2:VOLT:READ', 'BackLength'=>1800}
                                ]
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-07',
                   'Windows' => [
                                 {Type=>'Plot', Name=>'DAQ:RUNFILESIZE', 'BackLength'=>1800},
                                ]
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-08',
                   'Windows' => [
                                 {Type=>'Plot', Name=>'DAQ:ExpTrig_Debug_0', 'BackLength'=>300},
                                 {Type=>'Plot', Name=>'DAQ:ExpTrig_Debug_1', 'BackLength'=>300},
                                 {Type=>'Plot', Name=>'DAQ:ExpTrig_Debug_2', 'BackLength'=>300},
                                 {Type=>'Plot', Name=>'DAQ:ExpTrig_Debug_3', 'BackLength'=>300}
                                ]
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-09',
                   'Windows' => [
                                 {Type=>'Plot', Name=>'DAQ:Trig:Level2_Out_0', 'BackLength'=>300},
                                 {Type=>'Plot', Name=>'DAQ:Trig:Level2_Out_1', 'BackLength'=>300},
                                 {Type=>'Plot', Name=>'DAQ:Trig:Level2_Out_2', 'BackLength'=>300},
                                 {Type=>'Plot', Name=>'DAQ:Trig:Level2_Out_3', 'BackLength'=>300}
                                ]
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-10',
                   'Windows' => [
                                 {Type=>'Plot', Name=>'DAQ:Livetime_vme-taps-trigger', 'BackLength'=>600},
                                 {Type=>'Plot', Name=>'DAQ:Livetime_vme-taps-baf-a',   'BackLength'=>600},
                                 {Type=>'Plot', Name=>'DAQ:Livetime_vme-taps-baf-b',   'BackLength'=>300},
                                 {Type=>'Plot', Name=>'DAQ:Livetime_vme-taps-baf-c',   'BackLength'=>300},
                                 {Type=>'Plot', Name=>'DAQ:Livetime_vme-taps-baf-d',   'BackLength'=>300},
                                 {Type=>'Plot', Name=>'DAQ:Livetime_vme-taps-baf-e',   'BackLength'=>300}
                                ]
                  },
                  {
                   'Hostname' => 'a2onlinedisplay-11',
                   'Windows' => [
                                 {Type=>'Plot', Name=>'DAQ:Livetime_vme-taps-baf-f', 'BackLength'=>600},
                                 {Type=>'Plot', Name=>'DAQ:Livetime_vme-taps-pwo',   'BackLength'=>600},
                                 {Type=>'Plot', Name=>'DAQ:Livetime_vme-taps-veto-ab',   'BackLength'=>600},
                                 {Type=>'Plot', Name=>'DAQ:Livetime_vme-taps-veto-cd',   'BackLength'=>600},
                                 {Type=>'Plot', Name=>'DAQ:Livetime_vme-taps-veto-ef',   'BackLength'=>600}
                                ]
                  }
                 ]
  };


# some default config options
# and provide nice help documentation
# some global variables, needed everywhere

my $man = 0;
my $help = 0;
my $verbose = 0;
my $warnings = 1;
my $exact = 0;
my $dispsetfile;
my $t; # telnet connection
my $SSH; # SSH connection for "do" mode

Getopt::Long::Configure(qw(gnu_getopt));
GetOptions(
           'help|h' => \$help,
           'man' => \$man,
           'verbose|v+' => \$verbose,
           'warnings|w!' => \$warnings,
           'exact|e+' => \$exact,
           'dispsetfile|d=s' => \$dispsetfile
          ) or pod2usage(2);
pod2usage(1) if $help;
pod2usage(-exitval => 0, -verbose => 2) if $man;

if ($verbose) {
  # always enable warnings if verbose
  $warnings = 1;
}

# this loads some additional dispsets
&LoadDispsetfile;


# jump to subroutine which handles the job,
# depending on the options
my $mode = shift @ARGV || "";

switch($mode) {
  case "activate" { &Activate; }
  case "do" { &Do; }
  case "list" { &List; }
  else {
    Logger("Supplied mode not recognized, try --help or --man");
  }
}

sub Activate {
  my $dispset = $ARGV[0] || "";
  Logger("Specified dispset $dispset not found")
    unless defined $dispsets->{$dispset};

  Logger("Can't be that exact!")
    if $exact>3;
  Logger("Activating display set $dispset",1);
  # define a shortcut to the actual dispset data
  my $d = $dispsets->{$dispset};

  my @threads = map { threads->create(\&WorkOnDisplay, $_) } @{$d->{Displays}};
  $_->join for @threads;

  Logger("Display set $dispset activated.",1);
}

sub Do {
  load Net::OpenSSH;
  # use shift to keep in the rest of @ARGV the cmd and its arguments
  my $dispset = shift @ARGV || "";
  my $cmd = join(" ", @ARGV);

  # some simple logic to catch the ALL keyword properly
  # and init the necessary SSH connections
  if ($dispset eq 'ALL') {
    foreach my $dispset (keys $dispsets) {
      InitSSH($dispsets->{$dispset})
    }
  } else {
    Logger("Specified dispset not found") unless $dispsets->{$dispset};
    InitSSH($dispsets->{$dispset})
  }

  # in $SSH all hosts are initialized,
  # now execute the given command
  my $pid;
  foreach my $host (keys $SSH) {
    my $ssh = $SSH->{$host};
    # the perl pipe formats the output (prepends the hostname)
    $pid->{$host} = $ssh->system({async=>1},
                                 "$cmd 2>&1".
                                 " | perl -pe 'chomp(\$h=`hostname -s`); printf(\"%-17s %s\",\$h);'");
  }

  # Wait and check result
  my @failed_hosts = WaitForPIDs($pid);
  if (@failed_hosts) {
    Logger("The command could not be executed on ".join(" ", @failed_hosts), -1)
  }
}


sub List {
  print "The following display sets are available:\n";
  foreach my $dispset (sort keys $dispsets) {
    printf("* %-10s\t%s\n",$dispset,,$dispsets->{$dispset}->{Description});
  }
}



# helper functions

sub WaitForPIDs {
  my $pid = shift;
  # wait until all SSH commands have been executed
  # and check exit code
  my $failed = {};
  foreach my $host (keys $pid) {
    waitpid($pid->{$host},0);
    # transform to original exit value, see perlvar $?
    $failed->{$host} = $? >> 8 if $?;
  }
  if (wantarray()) {
    return keys $failed;
  } else {
    return $failed;
  }
}


sub InitSSH {
  my $dispset = shift;
  Logger("Initializing SSH connections in parallel...",2);
  foreach my $d (@{$dispset->{Displays}}) {
    my $host = $d->{Hostname};
    # check if there is already a connection
    next if exists $SSH->{$host};
    Logger("Opening connection to $host",3);
    # open the ssh connections for each host
    $SSH->{$host} = Net::OpenSSH->new($host, async => 1);
  }
}


sub LoadDispsetfile {
  return unless defined $dispsetfile;
  do $dispsetfile or Logger("Can't load dispsetfile $dispsetfile: $!",-1);
}


sub WorkOnDisplay {
  my $disp = shift;
  my $host = $disp->{Hostname};
  Logger("$host: Opening connection",2);

  $t = Net::Telnet->new(Timeout => 5, Port => 1337);
  $t->errmode("return");

  $t->open($host)
    or Logger("$host: Cannot open telnet connection: ".$t->errmsg);

  $t->waitfor(String => 'Welcome to PiGLET!')
    or Logger("$host: Did not receive welcome message: ".$t->errmsg);

  Logger("$host: Successfully connected.",2);

  # get the current windows via the List command.
  # search for words ending in _Remove, which
  # indicate the unique name of each window
  $t->print('List') or
    Logger("$host: Cannot send List command: ".$t->errmsg);
  $t->waitfor(String => 'Available commands:') or
    Logger("$host: Did not receive list of windows: ".$t->errmsg);
  my @words = split(/\s+/,$t->lastline);
  my %wins =
    map { s/(.+)_Remove/$1/ ? ($_ => 1) : () } @words;

  my $n = scalar keys %wins;
  my $n_exp = scalar @{$disp->{Windows}};
  Logger("$host: Currently $n windows active, $n_exp in display set.",2);

  if($exact==3) {
    Logger("$host: Removing all windows.",2);
    ExecCmd("RemoveAllWindows");
  }

  # let's check if it complies with the display set
  foreach my $win (@{$disp->{Windows}}) {
    my $name = $win->{Name}; # unique identier for windows
    if($exact==3 || !exists $wins{$name}) {
      Logger("$host: Window $name not found. Adding it.", 2);
      my $type = $win->{Type}; # Plot, Image, ...?
      ExecCmd("Add${type}Window $name");
      SetWindowProperties($win);
    }
    elsif($exact>=1) { # equals $exact==1 || $exact==2
      # ensure correct properties
      SetWindowProperties($win);
    }
    delete $wins{$name};
  }

  # remove "unknown" windows
  if($exact == 2) {
    foreach my $name (keys %wins) {
      Logger("$host: Removing unknown window $name", 2);
      ExecCmd("${name}_Remove");
    }
  }
}

sub SetWindowProperties {
  my $win = shift;
  my $host = $t->host;
  my $name = $win->{Name};
  foreach my $prop (keys $win) {
    # skip "special" properties
    next if $prop eq 'Name';
    next if $prop eq 'Type';
    my $val = $win->{$prop};
    # if the value is an arrayref, join all
    # values to a space-separated string
    if (ref $val eq 'ARRAY') {
      $val = join(' ',@$val);
    }
    Logger("$host: Setting ${name}_$prop to $val.", 2);
    ExecCmd("${name}_$prop $val");
  }
}

sub ExecCmd {
  my $host = $t->host;
  my $cmd = shift;
  #$t->dump_log('STDERR');
  $t->print($cmd) or
    Logger("$host: Cannot send Cmd '$cmd': ".$t->errmsg);
  my $patt = '/(Ok.|Error:)/';
  $t->waitfor($patt) or
    Logger("$host: Did not receive 'Ok.' or 'Error:' after '$cmd': ".$t->errmsg);
  if($t->lastline =~ /Error:/) {
    Logger("Cmd '$cmd' was not successful: ".$t->lastline);
  }
}

sub Logger {
  my $msg = shift;
  my $lvl = shift || 0; # 0=fatal, -1=warning, >0 verbose messages

  if ($lvl==0) {
    print STDERR "Error: $msg\n";
    exit 1;
  }
  elsif($lvl==-1) {
    print STDERR "Warning: $msg\n" if $warnings;
  }
  else {
    print "$msg\n" if $lvl<=$verbose;
  }
}

__END__

=head1 NAME

PiGLETManager - Interface to PiGLET (Raspberry Pi OpenGL EPICS Toolkit)

=head1 SYNOPSIS

PiGLETManager activate <displayset>

PiGLETManager do <displayset|ALL> <command>

PiGLETManager list

 Options:
   -h, --help         brief help message
   -v, --verbose      be verbose to STDERR
   -w, --warnings     print warnings to STDERR
   -e, --exact        set display more precisely
   -d, --dispsetfile  load extra display sets

=head1 OPTIONS

=over 8

=item B<--help>

Print a brief help message and exits.

=item B<--verbose>

Print some information what is going on. Default off.

=item B<--warnings>

Print warnings (or "not-so-fatal" errors). Default on.

=item B<--exact>

If specified once, the BackLength's are reset even if the window
exists.

If specified twice (e.g. -ee), then additionally unknown windows are
removed.

If specified three times (e.g. -eee), everything is removed and added
from scratch. Note: This also deletes the history of the graphs and
reloads the metadata like yrange from EPICS.

=item B<--dispsetfile>

Load more dispsets from the specified file. It must be valid PERL
syntax which extends or modifies the hashref $dispsets, see the
beginning of the source code of PiGLETManager how the default dispsets
are defined.


=back

=head1 DESCRIPTION

=cut
